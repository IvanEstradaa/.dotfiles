#!/usr/bin/osascript

on run argv
  set action to item 1 of argv -- First argument
  set fileAliases to {}

  -- Ensure Nextcloud is running, or launch it
  try
    do shell script "pgrep -f Nextcloud"
  on error
    -- Launch Nextcloud
    do shell script "open -a Nextcloud"
    delay 2 -- Give it a moment to start
  end try

  -- Iterate over remaining arguments (file paths)
  repeat with i from 2 to count of argv
    set filePath to item i of argv

    -- Check if file is inside Nextcloud's localPath
    set checkCommand to "file_path=" & quoted form of filePath & "; " & Â¬
      "if [[ ! $file_path == *\"$(grep localPath ~/Library/Preferences/Nextcloud/nextcloud.cfg | awk -F'localPath=' '{print $2}')\"* ]]; then exit 1; fi"

    try
      do shell script checkCommand
    on error
      display dialog "File is not in a Nextcloud-synced folder: " & filePath buttons {"OK"} default button 1
      return
    end try

    -- Add file to list for revealing
    copy (POSIX file filePath as alias) to the end of fileAliases
  end repeat

  -- Reveal files in Finder
  tell application "Finder" to reveal fileAliases

  -- Call Hammerspoon with the action
  do shell script "hs -c 'nextcloudFile(\"" & action & "\")'"
end run

